buildscript {
    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/tkob/maven' }
    }
    dependencies {
        classpath 'yokohama.unit:yokohamaunit:0.2.1'
    }
}

apply plugin: 'scala'
apply plugin: 'application'

mainClassName = 'nbmtools.Main'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://dl.bintray.com/tkob/maven' }
}

configurations {
    providedCompile
}

dependencies {
    providedCompile 'org.projectlombok:lombok:1.14.8'

    compile 'org.scala-lang:scala-library:2.11.7'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-collections4:4.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.scalatest:scalatest_2.11:2.2.5'
    testRuntime 'org.scala-lang.modules:scala-xml_2.11:1.0.5'
    testCompile 'org.codehaus.groovy:groovy-all:2.3.7'
    compile 'yokohama.unit:yokohamaunit-annotations:0.2.1'
}

compileJava {
    sourceSets.main.compileClasspath += configurations.providedCompile
    sourceSets.test.compileClasspath += configurations.providedCompile
}

task compileDocy << {
    ext.classpath = configurations.testCompile.join(File.pathSeparator) +
            File.pathSeparator + sourceSets.main.output.classesDir +
            File.pathSeparator + sourceSets.test.output.classesDir
    ext.classesDir = sourceSets.test.output.classesDir
    ext.baseDir = "$projectDir/src/test/docy"
    ext.sourceSet = fileTree(dir: ext.baseDir, include: '**/*.docy')
    ext.args = [
            'docyc',
            '-cp', ext.classpath,
            '-d', ext.classesDir,
            '-basedir', ext.baseDir,
            '-j',
            '-converter', 'nbmtools,java'
    ]
    ext.args += ext.sourceSet.getFiles()

    if (!ext.classesDir.exists()) {
        ext.classesDir.mkdirs()
    }
    def main = new yokohama.unit.Main(new yokohama.unit.CommandFactory());
    main.run(System.in, System.out, System.err, ext.args as String[])
}
compileDocy.dependsOn compileTestJava
test.dependsOn compileDocy

task groovyConsole(dependsOn: 'classes', type: JavaExec) {
    main = 'groovy.ui.Console'
    classpath = sourceSets.test.runtimeClasspath
}
